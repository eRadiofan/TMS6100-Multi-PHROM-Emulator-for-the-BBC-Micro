   10 REM >SBOUNDS
   20 DIM CODE% 100
   30 DIM DTA1% 5
   40 FOR N=0 TO 2 STEP 2
   50   P%=CODE%
   60   [ OPT N
   70   .REVERSE
   80   STA &80
   90   ROL &80
  100   ROR A
  110   ROL &80
  120   ROR A
  130   ROL &80
  140   ROR A
  150   ROL &80
  160   ROR A
  170   ROL &80
  180   ROR A
  190   ROL &80
  200   ROR A
  210   ROL &80
  220   ROR A
  230   ROL &80
  240   ROR A
  250   RTS
  260   .SETADDR LDA #&9F
  270   LDX #0
  280   .LOOP1
  290   LDY DTA1%,X
  300   JSR &FFF4
  310   INX
  320   CPX #5
  330   BNE LOOP1
  340   RTS
  350   .CMD LDA#&9F
  360   JSR &FFF4
  370   RTS
  380   .RDP LDA #&9F
  390   LDY #&10
  400   JSR &FFF4
  410   LDA #&9E
  420   JSR &FFF4
  430   STY DTA1%
  440   RTS
  450   ]
  460 NEXT
  470
  480 PROCSETADDR(0,&3FFE)
  490 IF FNREAD <> 0 THEN PROCERR(1)
  500 DAT% = FNREAD
  510 IF DAT% <> 0 THEN PROCERR(2)
  520 IF FNREAD <> &FF THEN PROCERR(3)
  530 IF FNREAD <> &FF THEN PROCERR(4)
  540 PRINT "Crossed boundary: PHROM 0->1, Not there"
  550
  560 PROCSETADDR(1,&3FFE)
  570 IF FNREAD <> &FF THEN PROCERR(1)
  580 IF FNREAD <> &FF THEN PROCERR(2)
  590 DAT% = FNREVERSE(FNREAD)
  600 IF DAT% <> &63 THEN PROCERR(3)
  610 DAT% = FNREVERSE(FNREAD)
  620 IF DAT% <> &A9 THEN PROCERR(4)
  630 PRINT "Crossed boundary: PHROM 1->2"
  640
  650 PROCSETADDR(4,&0FFE)
  660 IF FNREAD <> 0 THEN PROCERR(1)
  670 IF FNREAD <> 0 THEN PROCERR(2)
  680 IF FNREAD <> &FF THEN PROCERR(3)
  690 IF FNREAD <> &FF THEN PROCERR(4)
  700 PRINT "Crossed boundary: PHROM 4 &1000"
  710
  720 PROCSETADDR(15,&3FFF)
  730 IF FNREVERSE(FNREAD) <> &FE THEN PROCERR(1)
  740 IF FNREVERSE(FNREAD) <> 0 THEN PROCERR(2)
  750 PRINT "Crossed boundary: PHROM 15->0"
  760
  770 PROCINDIRECT(3,&3FFE)
  780 IF FNREAD <> &FF THEN PROCERR(1)
  790 DAT% = FNREVERSE(FNREAD)
  800 IF DAT% <> &FF THEN PRINT ~DAT%:PROCERR(2)
  810 DAT% = FNREVERSE(FNREAD)
  820 IF DAT% <> &FF THEN PRINT ~DAT%:PROCERR(3)
  830 PRINT "OOB indirect compatibility 1"
  840
  850 PROCSETADDR(3,&3FFD)
  860 IF FNREAD <> &FF THEN PROCERR(1)
  870 Y% = &30
  880 CALL CMD
  890 IF FNREAD <> &FF THEN PROCERR(2)
  900 IF FNREAD <> &FF THEN PROCERR(3)
  910 IF FNREAD <> &FF THEN PROCERR(4)
  920 PRINT "OOB indirect compatibility 2"
  930
  940 PRINT "Boundary tests OK"
  950 PROCPHRASE_D(0,162)
  960 END
  970
  980 DEF PROCERR(ER%)
  990 PRINT "TEST FAILED AT STEP: ";ER%
 1000 PROCPHRASE_D(0,155)
 1010 END
 1020 ENDPROC
 1030
 1040 DEF FNREVERSE(A%)
 1050 =USR(REVERSE) AND &FF
 1060
 1070 DEF FNREAD
 1080 CALL RDP
 1090 =?DTA1%
 1100
 1110 DEF PROCSETADDR(RN%,PA%)
 1120 DTA1%?0=&40+(PA% AND &000F)
 1130 DTA1%?1=&40+((PA% AND &00F0) DIV 16)
 1140 DTA1%?2=&40+((PA% AND &0F00) DIV 256)
 1150 DTA1%?3=&40+(RN% AND 3)*4+((PA% AND &3000) DIV &1000)
 1160 DTA1%?4=&40+(RN% AND 12)/4
 1170 CALL SETADDR
 1180 ENDPROC
 1190
 1200 DEF PROCPHRASE_D(RN%,PN%)
 1210 PA%=2 * PN%
 1220 PROCSETADDR(RN%,PA%)
 1230 LO% = FNREVERSE(FNREAD)
 1240 HI% = FNREVERSE(FNREAD)
 1250 PA% = LO% + 256*HI%
 1260 PROCSETADDR(RN%,PA%)
 1270 Y%=&50
 1280 CALL CMD
 1290 ENDPROC
 1300
 1310 DEF PROCINDIRECT(RN%,PA%)
 1320 PROCSETADDR(RN%,PA%)
 1330 Y%=&30
 1340 CALL CMD
 1350 ENDPROC
